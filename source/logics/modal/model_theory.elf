%* Model theory of modal logic using the Kripke universes
   @author Florian Rabe *%
   
%namespace "http://latin.omdoc.org/logics/model_theory/kripke".
%namespace syn = "../syntax".
%namespace minimal = "../../foundations/minimal".


%sig Kripke = {
  %include minimal.STTIFOLEQExt.
  %include minimal.Universes.
  
  world : set.
  %% accessibility relation
  acc'  : elem (world → world → bool').
  acc   : elem world -> elem world -> elem bool' = [v][w] acc' @ v @ w.
  exists_world : ded exists [x : elem world] true.
}.

%view Base : syn.Logic -> Kripke = {
  o   := elem (world → bool').
  ded := [f] ded (∀[w] f @ w) eq 1.
}.

%view MPL : syn.MPL -> Kripke = {
  %include Base.
  ⊥ := λ[w] 0.
  ⇒ := [f][g] λ[w] (f @ w) ⇒ (g @ w).
}.

%view Necessity : syn.Necessity -> Kripke = {
  %include Base.
  □ := [f] λ[w] ∀[w'] (acc w w') ⇒ (f @ w').
}.

%view Possibility : syn.Possibility -> Kripke = {
  %include Base.
  ◇ := [f] λ[w] ∃[w'] (acc w w') ∧ (f @ w').
}.

%view ML : syn.ML -> Kripke = {
  %include MPL.
  %include Necessity.
  %include Possibility.  
}.
