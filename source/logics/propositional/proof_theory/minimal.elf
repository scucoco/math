%namespace "http://latin.omdoc.org/logics/proof_theory".
%namespace syn = "syntax".

%sig MPL = {
  %include syn.MPL %open.
  syllog : ded (P ⇒ Q) ⇒ (Q ⇒ R) ⇒ (P ⇒ R).
  peirce : ded ((P ⇒ Q) ⇒ P) ⇒ P.
  weaken : ded P ⇒ (Q ⇒ P).
  contra : ded ⊥ ⇒ P.
  mp     : ded (P ⇒ Q) -> ded P -> ded Q.
}.  

%view MPL-CPL : MPL -> CPL = {
  %include syn.MPL-PL.
  syllog := [P][Q][R] impI [pq] impI [qr] impI [p] impE qr (impE pq p).
  peirce := [P][Q] impI [pqp] orE tnd ([p : ded P] p)
                                      ([np : ded not P] impE pqp (impI [p: ded P] notE np p Q)).
  weaken := [P][Q] impI [p] impI [q] p.
  contra := [P] impI [f] falseE f P.
  mp     := [P][Q][p][q] impE p q.
}.